buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'io.github.fvarrui:javapackager:1.6.2'
	}
}

apply plugin: 'io.github.fvarrui.javapackager.plugin'

def props = new Properties()
file("../cnf/version.bnd").withInputStream { props.load(it) }

def git_branch() {
    def branch = ""
    def proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch
}

def project_version = props.getProperty("version")
def current_branch = git_branch()

if (!current_branch.startsWith("release")) {
	project_version = project_version + "." + new Date().format('yyyyMMddHHmmss')
}

javapackager {
	name                = 'OSGi.fx'
	version             = project_version
	description         = 'An easy-to-use desktop application to manage OSGi frameworks remotely'
	mainClass           = 'aQute.launcher.pre.EmbeddedLauncher'
	bundleJre           = true
	runnableJar         = file('osgifx.jar')
	copyDependencies    = false
	organizationName    = 'OSGi.fx'
	organizationUrl     = 'https://osgifx.com'
	organizationEmail   = 'admin@amitinside.com'
	jrePath             = file(System.properties['java.home'])
	url                 = 'https://osgifx.com'
	macConfig {
		appId           = 'com.osgifx'
		icnsFile        = file('icons/macosx/OSGi.fx.icns')
		createTarball   = true
	}
	linuxConfig {
		pngFile         = file('icons/linux/OSGi.fx.png')
		createTarball   = true
	}
	winConfig {
		icoFile         = file('icons/macosx/OSGi.fx.ico')
		generateMsi     = false
	}
}

task('package_distro', type: io.github.fvarrui.javapackager.gradle.PackageTask) {
	platform        'auto'
}